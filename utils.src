// Check if a list contains a specific element
// @param {string} needle The element to search for
// @return {bool} true if the element is found, false otherwise
list.contains = function(needle)
    return self.indexOf(needle) != null 
end function

// Push multiple items into the list
// @param {list} items The items to push into the list
list.pushMany = function(items)
    if self == items then return // Prevent pushing self into self

    for item in items
        self.push(item)
    end for
end function

// Filter the list based on a provided function
// @param {function} filterFunc The function to determine if an item should be included
// @returns {list} A new list containing only the items that pass the filter
list.where = function(filterFunc)
    result = []
    for item in self
        if filterFunc(item) then
            result.push(item)
        end if
    end for
    return result
end function

// Check if the string starts with the specified prefix
// @param {string} prefix The prefix to check
// @return {bool} true if the string starts with the prefix, false otherwise
string.startsWith = function(prefix)
    return self.len >= prefix.len and self.indexOf(prefix) == 0
end function

// Check if the string ends with the specified suffix
// @param {string} suffix The suffix to check
// @return {bool} true if the string ends with the suffix, false otherwise
string.endsWith = function(suffix)
    return self.len >= suffix.len and self[-suffix.len:] == suffix
end function

// Convert the number to a boolean string ("true" or "false")
number.toBoolString = function()
    if self != 0 then
        return "true"
    else
        return "false"
    end if
end function

Math = {}
Math.min = function(a, b)
    if a < b then
        return a
    else
        return b
    end if
end function

Math.max = function(a, b)
    if a > b then
        return a
    else
        return b
    end if
end function